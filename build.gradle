plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'com.diffplug.spotless' version '6.21.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'org.sonarqube' version '4.3.0.3225' // SonarQube plugin
}
jacoco {
	toolVersion = "0.8.10" // Specify the JaCoCo version
}
group = 'org.services'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.projectlombok:lombok'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	runtimeOnly 'org.postgresql:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.liquibase:liquibase-core'
	implementation 'software.amazon.awssdk:s3:2.20.12'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
	implementation 'org.glassfish.expressly:expressly:5.0.0'
}
spotless {
	java {
		target '**/*.java'
		googleJavaFormat()
		removeUnusedImports()
	}
}


tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}
jacocoTestReport {
	dependsOn test // Ensure tests run before coverage report is generated

	reports {
		xml.required = true
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}
//TODO: increase test coverage ratio to 0.50
jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.00
			}
		}
	}
}
tasks.named('check') {
	dependsOn 'jacocoTestCoverageVerification'
}

tasks.named('sonarqube').configure {
	dependsOn jacocoTestReport // Ensure JaCoCo runs before SonarQube
}
tasks.named('build') {
	dependsOn 'spotlessCheck'
}
